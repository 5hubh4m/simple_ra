SAss.   Stm ::= Ident "<-" Exp ;
SExp.   Stm ::= Exp ;

ETup.   Exp ::= Tup ;
EIdt.   Exp ::= Ident ;
ESel.   Exp ::= "SELECT" "[" Prd "]" "(" Exp ")" ;
ERen.   Exp ::= "RENAME" "[" [Ident] "]" "(" Exp ")" ;
EPrj.   Exp ::= "PROJECT" "[" [Ident] "]" "(" Exp ")" ;
EBin.   Exp ::= "(" Exp ")" Bin "(" Exp ")" ;

separator nonempty Ident "," ;

rules   Cmp ::= "=?" | "/=?" | "<=?" | ">=?" | "<?" | ">?" ;
rules   Bul ::= "|" | "&" ;
rules   Bin ::= "X" | "^" | "v" | "-" | "@" ;

VDbl.   Val ::= Double ;
VInt.   Val ::= Integer ;
VSTr.   Val ::= String ;

PSmp.   Prd ::= Ident Cmp Val ;
PNot.   Prd ::= "(" "~" Prd ")" ;
PCom.   Prd ::= "(" Prd ")" Bul "(" Prd ")" ;

Sch1.   Sch ::= Ident ":" Val ;

Tup1.   Tup ::= "{" [Sch] "}" ;

separator nonempty Sch "," ;
